;; SPDX-License-Identifier: SSPL-1.0
;; Copyright © 2026 Vaelii LLC and the Vaelii contributors.
(ns vaelii.impl.discovery
  "The detector of the one nogood family a settle finds: the clashes argument preservation
  infers between a stored claim and a known-true claim no one stored, each with the most
  general contexts that read it whole, where `chain/place-inherited!` places it.  The
  memo `:preserved-clashes` carries a clash whose inputs did not move.  See docs/nmtms.md,
  \"The inherited-clash memo\"."
  (:require [taoensso.trove :as trove]
            [vaelii.impl.caches :as caches]
            [vaelii.impl.capabilities :as cap]
            [vaelii.impl.decide :as decide]
            [vaelii.impl.decide.inherited :as inherited]
            [vaelii.impl.inherit :as inherit]
            [vaelii.impl.jtms :as jtms]
            [vaelii.impl.kb :as kb]
            [vaelii.impl.naming :as nm]
            [vaelii.impl.protocols :as p]
            [vaelii.impl.reads :as reads]
            [vaelii.impl.resolution :as res]
            [vaelii.impl.sentex :as sx]
            [vaelii.impl.special :as special]
            [vaelii.impl.strength :as strength]
            [vaelii.impl.taxonomy :as tax]
            [vaelii.impl.types.reasoning :as reasoning]))

(def ^:dynamic *whole-store-region?*
  "True while `recover` runs the settle that follows `rebuild-tms`, whose region is the
  whole store.  `preserving-entries` skips its store-wide sweep under it, and only
  when the region is also at least the store's sentex count (docs/nmtms.md, \"Which
  entry point the content came through\")."
  false)

(def ^:dynamic *incremental-preserving*
  "True (the default): `preserving-nogoods` carries each standing inherited clash whose
  inputs did not move, and re-asks only the stored facts a moved claim reaches.  False:
  every call re-asks every standing clash and the whole stored extent of every predicate
  a claim moved, the reference `inherited_clash_oracle_test` compares the memo against."
  true)

(defn- flat-reading
  "`[reading moved]`: the flat caches as `kb` reads them, `{:pos :map}`, and the
  flat-cache keys whose supporting contexts moved since `prev`, an earlier reading.  `:pos`
  is the taxonomy's position in the journal `tax/flat-moves` reads, and `:map` the
  contexts map the discovery reads (`tax/flat-contexts`).  The moves are the journal's
  since `prev`'s position.  Where the journal does not reach back that far (an
  image install, a rebuild, a journal restarted past its bound) they are the keys the
  two maps differ on, and nil when `prev` holds no map."
  [kb prev]
  (let [tax         (reasoning/taxonomy kb)
        [pos moved] (tax/flat-moves tax (:pos prev))
        now         (tax/flat-contexts tax)
        was         (:map prev)]
    [{:pos pos :map now}
     (cond moved                       moved
           (identical? was now)        #{}
           (some? was)                 (-> #{}
                                           (into (keep (fn [[k cs]] (when-not (= cs (get was k)) k)))
                                                 now)
                                           (into (remove #(contains? now %)) (keys was))))]))

(defn- member-priority
  "The rebuttal-range priority of an inherited nogood: the highest defeat-class rank among
  `members`."
  [tms members]
  (reduce max (map #(strength/rank-of (jtms/defeat-class tms %)) members)))

(defn- inherited-nogoods
  "The nogood maps `{:nogood :priority :vantages :kind :sentence :inherited}` for stored
  claim `h`, stating `sen`, against the `inherit/clashing-claim` answers `cs`: one per
  member set, its `:vantages` the most general of the contexts that found it."
  [kb h sen cs]
  (let [tms (reasoning/tms kb)
        tax (reasoning/taxonomy kb)]
    (mapv (fn [[members hits]]
            (let [c     (first hits)
                  [a b] (sort nm/compare-form [(:sentence c) sen])
                  vs    (into #{} (map :context) hits)]
              {:nogood   members
               :priority (member-priority tms members)
               :vantages (if (next vs) (tax/maximal-contexts tax vs) vs)
               :kind     :inherited
               :sentence (list 'contradicts a b)
               ;; the claim nobody stored, where it was read, the general `:claim` it was
               ;; read from and the handles `:via` which it was carried
               :inherited {:sentence (:sentence c) :context (:context c)
                           :claim (:claim c) :via (vec (:handles c))}}))
          (group-by #(into #{h} (cons (:claim %) (:handles %))) cs))))

(defn- preserving-entry
  "`s`'s inherited clash, or nil: `{:pred :askers :reads :contexts :classes :class
  :nogoods}`.  The clashing claims are `inherit/clashing-claim`'s answers from `s`'s own
  context and from the vantages of each reading `inherit/denial-readings` names: the
  maximal common descendants of `s`'s context and the reading's where `s` and every
  handle of the reading are seen with no except hiding one
  (`res/exception-aware-placements`, the placement a firing takes).  `:askers` is every
  context asked, `:reads` `s` and every handle of the readings, `:contexts` the contexts
  they are stated in, `:classes` the defeat-class of every member but `s`, `:class` `s`'s
  own, and `:nogoods` `inherited-nogoods`.  An answer of no clash is an entry with no
  nogoods while some claim would deny `s` in a reader that saw both, so an except or a
  `genlCx` edge that makes that reader asks it again (`preserving-entries`)."
  [kb s]
  (let [sen    (:sentence s)
        tms    (reasoning/tms kb)
        c      (:context s)
        denial (inherit/denial-readings kb sen)
        askers (cons c (sort (disj (into #{}
                                         (mapcat (fn [{:keys [handles contexts]}]
                                                   (res/exception-aware-placements
                                                    kb (conj handles (:id s))
                                                    (into [c] (sort (disj contexts c))))))
                                         denial)
                                   c)))
        cs     (into [] (mapcat #(keep (fn [f] (f kb sen %))
                                       [inherit/clashing-claim inherit/converse-claim]))
                     askers)]
    (when (or (seq cs) (seq denial))
      {:pred     (nm/functor (kb/body-under-not sen))
       :askers   (set askers)
       :reads    (into #{(:id s)} (mapcat :handles) denial)
       :contexts (into #{c} (mapcat :contexts) denial)
       :classes  (into {} (map (juxt identity #(jtms/defeat-class tms %)))
                       (mapcat #(cons (:claim %) (:handles %)) cs))
       :class    (jtms/defeat-class tms (:id s))
       :nogoods  (inherited-nogoods kb (:id s) sen cs)})))

(defn note-removed!
  "Record `sentex`, leaving the store, as `{handle sentence}` under `:left` in
  `:preserved-clashes` while the memo holds an entry, so the next `preserving-entries`
  reads what a handle without a record moved.  `preserving-nogoods` empties `:left` with
  the rest of the memo.  Called from `integrate/sentex-removed!`."
  [kb sentex]
  (let [memo (reasoning/preserved-clashes kb)]
    (when (seq (:entries @memo))
      (swap! memo assoc-in [:left (:id sentex)] (sx/sentence-of sentex)))))

(defn- preserving-moves
  "What the moved handles `moves` (`[handle region?]`, `region?` false for a withdrawal
  alone) re-open, as `[whole reached]`: the preserved predicates whose stored extent is
  re-asked (`inherit/moved-channels`' other channels, and a `genl` edge between
  predicates), and the stored facts a moved claim reaches (`inherit/claim-reach-extent`).
  A claim reaches when it is believed and known-true,
  and, moved in the region, when `vantaged` (a delay) holds its predicate: the claims of every
  class decide which vantages ask.  `narrow?` false re-opens the stored extent of every
  predicate a claim moves.  A handle with no record is read from `left`, the sentences
  `note-removed!` recorded: it is OUT, so it reaches as a claim moved in the region.  The
  `genl` closures are global, since what a moved edge re-opens is asked from every context
  that reads it."
  [kb pairs left moves vantaged narrow?]
  (let [recs   (:records kb)
        tms    (reasoning/tms kb)
        tax    (reasoning/taxonomy kb)
        extent (memoize #(reads/as-stored-with-functor (:index kb) %))
        preds  (into #{} (map first) pairs)
        ;; a functor whose channels read no argument answers once per call
        by-f   (memoize #(inherit/moved-channels kb (list %) pairs any?))]
    (inherit/with-memo
      (reduce
       (fn [[whole reached] [h region?]]
         (if-let [sen (if-let [s (p/get-sentex recs h)] (sx/sentence-of s) (get left h))]
           (let [body  (kb/body-under-not sen)
                 f     (nm/functor body)
                 [claimed other]
                 (if (inherit/channels-read-arguments? f)
                   (inherit/moved-channels
                    kb sen pairs #(and (not (contains? whole %)) (seq (extent %))))
                   (by-f f))
                 ;; a `genl` edge between predicates makes one's claims the other's, and
                 ;; carries a mark over to a sub-predicate (`tax/props-over`)
                 whole (cond-> (into whole other)
                         (and (= 'genl f) (every? symbol? (nm/args body)))
                         (into (filter preds) (mapcat #(tax/genls-global tax %) (nm/args body))))
                 kt?   (delay (and (jtms/in? tms h)
                                   (strength/known-true? (jtms/defeat-class tms h))))]
             [whole
              (into reached
                    (comp (remove whole)
                          (filter #(or (not narrow?) (and region? (contains? @vantaged %)) @kt?))
                          (mapcat #(or (when narrow? (inherit/claim-reach-extent kb sen %))
                                       (extent %))))
                    claimed)])
           [whole reached]))
       [#{} #{}]
       moves))))

(defn- entry-live?
  "Does every member of entry `e` but its candidate hold the defeat-class `e` recorded?"
  [tms e]
  (every? (fn [[h c]] (and (jtms/in? tms h) (= c (jtms/defeat-class tms h)))) (:classes e)))

(defn- except-reach
  "The handles an `except` that moved can hide or show, as a set: what `special/except-moved`
  queued, the target of each `except` among the region `handles` (read from `left` for one
  with no record), each of those followed down its except cascade, and their consequence
  closure.  nil when no except moved."
  [kb handles left]
  (let [recs   (:records kb)
        target #(some-> (if-let [s (p/get-sentex recs %)] (sx/sentence-of s) (get left %))
                        kb/except-target)
        roots  (into (set (special/except-moved kb)) (keep target) handles)]
    (when (seq roots)
      (jtms/consequence-closure
       (reasoning/tms kb)
       (into #{} (mapcat #(take-while some? (iterate target %))) roots)))))

(defn- reached-by-edges?
  "Can the `genlCx` move `reach` (`decide/edge-reach`) change entry `e`'s askers: a context
  it reads is in `:below`, or a context it asked is in `:under`?"
  [reach e]
  (boolean (or (some (:below reach) (:contexts e)) (some (:under reach) (:askers e)))))

(defn- preserving-entries
  "The standing inherited clashes after this call, `{handle entry}` (`preserving-entry`).
  An entry is carried when the `genlCx` relation was not rebuilt and `prev` holds its
  generation, the flat-cache keys that
  moved are known (`flat-reading`'s `moved`), every handle in the region without a record
  has its sentence in `:left` (`note-removed!`), its own handle is not in `:left`, its
  predicate's extent was not re-opened, no moved claim reaches it (`preserving-moves`,
  over the region and the supporters of each moved key), no except that moved reaches a
  handle it reads (`except-reach`), no `genlCx` move since `gen` reaches it
  (`reached-by-edges?`), and its members hold the classes it recorded.  Every other entry,
  the region, and what the moves re-open are asked.  An entry whose candidate is OUT is
  kept and publishes nothing.  The region is what the touched window recorded since the
  last call's mark (`jtms/touched-since`), so a later pass of one settle asks only what the
  passes before it moved, and `region`, the settle's whole window, when the mark is from an
  earlier window.  Under `*whole-store-region?*` with a region the store's size, nothing is
  carried, no extent is swept, and the questions are the preserved predicates' stored
  extents (docs/nmtms.md, \"The inherited-clash memo\")."
  [kb pairs region prev gen moved]
  (let [recs    (:records kb)
        tms     (reasoning/tms kb)
        tax     (reasoning/taxonomy kb)
        handles (if-some [m (when *incremental-preserving* (:mark prev))]
                  (jtms/touched-since tms m)
                  @region)
        entries (:entries prev {})
        left    (:left prev {})
        reach   (when (and (:gen prev) (not= gen (:gen prev)))
                  (decide/edge-reach tax (:gen prev)))
        whole?  (and *whole-store-region?* (>= (count handles) (cap/count-sentexes recs)))
        why     (cond whole?                          :whole-store
                      (not *incremental-preserving*)  :exhaustive
                      (or (:all? reach) (nil? moved)) :rebuilt
                      ;; an image an earlier build wrote holds entries and no generation
                      (and (seq entries) (nil? (:gen prev))) :rebuilt
                      ;; nothing reads what a handle with no record and no `left` sentence moved
                      (some #(and (not (contains? left %)) (nil? (p/get-sentex recs %))) handles)
                      :retraction)
        fresh?  (some? why)
        [whole reached]
        (if whole?
          [#{} #{}]
          (preserving-moves kb pairs left
                            ;; a moved flat-cache entry is read through what its supporters state
                            (concat (map #(vector % true) handles)
                                    (when-not fresh?
                                      (map #(vector % false)
                                           (into #{}
                                                 (mapcat #(tax/visible-supporters tax % nil))
                                                 moved))))
                            (delay (into #{} (map (comp :pred val)) entries))
                            *incremental-preserving*))
        hidden  (when-not fresh? (except-reach kb handles left))
        dropped (if fresh?
                  (keys entries)
                  (into [] (keep (fn [[h e]]
                                   (when-not (and (not (contains? whole (:pred e)))
                                                  (not (contains? reached h))
                                                  (not (contains? left h))
                                                  (not (and hidden (some hidden (:reads e))))
                                                  (not (and reach (reached-by-edges? reach e)))
                                                  (entry-live? tms e))
                                     h)))
                        entries))
        kept    (if fresh? {} (persistent! (reduce dissoc! (transient entries) dropped)))
        idx     (:index kb)
        preds   (into #{} (map first) pairs)
        ;; the whole store's claims are the preserved predicates' stored extents, read
        ;; off the index rather than off a record per region handle
        ask     (-> (set dropped)
                    (into (if whole? (mapcat #(reads/as-stored-with-functor idx %) preds) handles))
                    (into reached)
                    (into (mapcat #(reads/as-stored-with-functor idx %)) whole)
                    (->> (remove kept)))]
    (trove/log! {:level :debug :id ::asked
                 :data  {:region (count handles) :carried (count kept) :asked (count ask)
                         :fresh why}})
    ;; one memo over every question: belief does not move while they are asked
    (inherit/with-memo
      (into kept
            (keep (fn [h]
                    (when (jtms/in? tms h)
                      (when-let [s (p/get-sentex recs h)]
                        (when (contains? preds (nm/functor (kb/body-under-not (:sentence s))))
                          (when-let [e (preserving-entry kb s)] [h e]))))))
            ;; sorted so the queries run in a reproducible order
            (sort ask)))))

(defn preserving-nogoods
  "The clashes between a stored claim and a known-true claim argument preservation reads
  at its own tuple, as nogood maps (`inherited-nogoods`).  Resets `:preserved-clashes`,
  the memo `preserving-entries` carries.

  The members are the stored claim and the reading's reasons (`inherit/clashing-claim`'s
  `:claim` and `:handles`).  `:priority` is the rebuttal range, 1–2.  A candidate is asked
  from its own context and from every vantage (`preserving-entry`).  A body stored in both
  polarities is a negation pair (`chain/place-negations!`), excluded by
  `clashing-claim`.  Behind `inherit/preserved-pairs`, two predicate-extent counts on a KB
  that declares none (docs/nmtms.md, \"What qualifies as a nogood\";
  docs/inherit.md).  The memo keeps a mark of the touched window taken before the
  questions (`jtms/touch-mark`), where the next call's region starts, and the `genlCx`
  generation, from which the next call reads the edges moved since (`decide/edge-reach`)."
  [kb region]
  (let [pairs (inherit/preserved-pairs kb)]
    (if (empty? pairs)
      (do (reset! (reasoning/preserved-clashes kb) {}) #{})
      (let [tms          (reasoning/tms kb)
            mark         (jtms/touch-mark tms)
            gen          (tax/relation-gen (reasoning/taxonomy kb) :genlCx)
            prev         @(reasoning/preserved-clashes kb)
            [flat moved] (flat-reading kb (:flat prev))
            entries      (preserving-entries kb pairs region prev gen moved)]
        (reset! (reasoning/preserved-clashes kb)
                {:gen     gen
                 :flat    flat
                 :mark    mark
                 :entries entries})
        (into #{}
              (mapcat (fn [[h e]]
                        (when (and (seq (:nogoods e)) (jtms/in? tms h))
                          ;; the other members hold the classes `e` recorded
                          (if (= (:class e) (jtms/defeat-class tms h))
                            (:nogoods e)
                            (map #(assoc % :priority (member-priority tms (:nogood %)))
                                 (:nogoods e))))))
              entries)))))

(defn discover-inherited!
  "Find the inherited clashes (`preserving-nogoods`) over the network and record them in
  the candidate index (`inherited/install-inherited!`), as `{:found [ngmap] :left
  [members]}`: each clash, whose members the network believes, of a candidate asked again
  this call (one whose memo entry was not carried), and the member sets the index held
  and no longer holds.  `chain/place-inherited!` places the first at their vantages and
  removes the placements of the second."
  [kb region]
  (let [tms  (reasoning/tms kb)
        in?  #(jtms/in? tms %)
        prev (:entries @(reasoning/preserved-clashes kb) {})
        ngs  (into [] (comp (filter #(every? in? (:nogood %)))
                            (filter #(seq (:vantages %)))
                            (distinct))
                   (preserving-nogoods kb region))
        now  (:entries @(reasoning/preserved-clashes kb) {})
        left (inherited/install-inherited! kb ngs)]
    ;; a carried entry is the identical value, so its clashes and their placements stand
    {:found (into [] (filter (fn [ng] (some #(not (identical? (get now %) (get prev %))) (:nogood ng))))
                  ngs)
     :left  left}))

;; ---- derived state (docs/caches.md, "The derived-state register") ----------------

(caches/register-derived
 {:id :D3 :label "Preserved clashes" :kind :cache :keyed-by :handle
  :reads [:index :T1 :Q3 :J4 :M2]
  :retired-by {:removed :K :inherited :K :settle-pass :K :recover :R :image-install :R}
  :computed :settle :imaged? :state :at [[:preserved-clashes]]
  :note "the inherited clashes found per stored claim with what each was read under; read again by region, moves, the excepts that moved and the genlCx edges moved since its generation, whole when J4 lost its position or genlCx was rebuilt, and `:left` notes a removal"})
