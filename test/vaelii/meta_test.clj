;; SPDX-License-Identifier: SSPL-1.0
;; Copyright © 2026 Vaelii LLC and the Vaelii contributors.
(ns vaelii.meta-test
  "The vocabulary context and meta-level features over the starter schema (with
  the test-world beneath it):

    * the context *spindle* — CxCore ⊏ upper ⊏ CxUniverse ⊏ middle ⊏ CxWell;
    * the predicate meta-ontology — predicates classified by arity and by the
      algebraic properties their metadata declares (each mark is itself a
      binary_predicate type, so the property is the classification);
    * decontextualized_predicate — a fact stated in one context deduced into
      CxUniverse and thereby visible everywhere."
  (:require [clojure.test :refer [deftest is testing use-fixtures]]
            [clojure.walk :as walk]
            [vaelii.core :as v]
            [vaelii.impl.checks :as checks]
            [vaelii.impl.decide :as decide]
            [vaelii.impl.sentex :as sx]
            [vaelii.impl.taxonomy :as tax]
            [vaelii.impl.types.reasoning :as reasoning]
            [vaelii.test-util :as tu]
            [vaelii.world :as world]))

(use-fixtures :once (tu/loaded (fn [kb] (-> kb tu/load-starter! world/load-into))))
(use-fixtures :each (tu/neutral))

(tu/deftest-kb the-context-spindle-core-upper-universe-middle-well
  (testing "upper rides between Core and Universe: an upper context sees Core, Universe sees it"
    (is (seq (v/sentexes-matching kb '(genlCx CxOrganism CxCore) '?ctx)))
    (is (seq (v/sentexes-matching kb '(genlCx CxUniverse CxOrganism) '?ctx))))
  (testing "middle rides between Universe and Well"
    (is (seq (v/sentexes-matching kb '(genlCx CxBiology CxUniverse) '?ctx)))
    (is (seq (v/sentexes-matching kb '(genlCx CxWell CxBiology) '?ctx))))
  (testing "there is no direct Well→Core edge, but Core is transitively visible from Well"
    (is (empty? (v/sentexes-matching kb '(genlCx CxWell CxCore) '?ctx)))
    (is (tax/sees? (reasoning/taxonomy kb) 'CxWell 'CxCore)))
  (testing "CxCore vocabulary is visible from the collector and the data contexts"
    (is (v/ask? kb '(binary_predicate genl) 'CxUniverse))
    (is (v/ask? kb '(binary_predicate parentOf) 'CxNaturalWorld)))
  (testing "the upper ontology rides in the upper contexts, not the collector"
    (is (seq   (v/sentexes-matching kb '(genl dog mammal) 'CxOrganism)))
    (is (empty? (v/sentexes-matching kb '(genl dog mammal) 'CxUniverse)))
    (is (seq   (v/sentexes-matching kb '(human Tom) 'CxNaturalWorld)))))

(tu/deftest-kb predicates-classified-by-arity
  (testing "unary — every type, and one-place properties"
    (is (v/isa? kb 'dog 'unary_predicate))
    (is (v/isa? kb 'thing 'unary_predicate))
    (is (v/isa? kb 'awake 'unary_predicate)))
  (testing "binary and ternary"
    (is (v/isa? kb 'parentOf 'binary_predicate))
    (is (v/isa? kb 'genl 'binary_predicate))
    (is (v/isa? kb 'arg 'ternary_predicate)))
  (testing "everything classified is a predicate, hence a thing"
    (is (v/isa? kb 'parentOf 'predicate))
    (is (v/isa? kb 'dog 'predicate))
    (is (v/isa? kb 'parentOf 'thing)))
  (testing "negatives"
    (is (not (v/isa? kb 'dog 'binary_predicate)))
    (is (not (v/isa? kb 'siblingOf 'unary_predicate)))))

(tu/deftest-kb an-asserted-arity-concludes-the-class-and-a-class-is-read-as-the-arity
  (testing "arity is itself a binary predicate"
    (is (v/isa? kb 'arity 'binary_predicate)))
  (testing "no rule concludes an arity from a class: arity is on the forced-monotonic roster"
    (is (empty? (v/sentexes-matching kb '(arity dog 1) '?ctx)))
    (is (empty? (v/sentexes-matching kb '(arity parentOf 2) '?ctx)))
    (is (empty? (v/sentexes-matching kb '(arity arg 3) '?ctx))))
  (testing "every reader of an arity reads the exact-class membership, in its context"
    (is (= 2 (:arity (v/describe kb 'parentOf 'CxLife))))
    (is (= 2 (:arity (v/describe kb 'siblingOf 'CxLife))))            ; symmetric -> binary
    (is (= 3 (:arity (v/describe kb 'arg 'CxCore))))
    (is (nil? (:arity (v/describe kb 'parentOf 'CxCore)))
        "a private declaration stays private")
    (is (v/ask? kb '(admitsArgnum parentOf 2) 'CxNaturalWorld))
    (is (not (v/ask? kb '(admitsArgnum parentOf 3) 'CxNaturalWorld))))
  (testing "an asserted arity concludes the relation-wide class, not the kind"
    (tu/with-terms [fooRelation]
      (v/assert kb (list 'arity fooRelation 2) 'CxCore)
      (is (v/isa? kb fooRelation 'binary) "the converse reaches the relation-wide class")
      (is (not (v/isa? kb fooRelation 'binary_predicate))
          "two arguments is a shape a function has too, so the kind stays open")
      (is (not (v/isa? kb fooRelation 'predicate)))
      (is (v/isa? kb fooRelation 'relation))
      (is (v/isa? kb fooRelation 'fixed_arity))
      (is (seq (v/sentexes-matching kb (list 'fixed_arity fooRelation) 'CxCore)))
      (is (seq (v/sentexes-matching kb (list 'binary fooRelation) 'CxCore)))))
  (testing "the fixed policy is justified by the arity fact and the rule"
    (tu/with-terms [barRelation]
      (v/assert kb (list 'arity barRelation 3) 'CxCore)
      (let [h (v/handle-of kb (list 'fixed_arity barRelation) 'CxCore)
            rule (v/handle-of kb '(implies (arity ?relation ?arity)
                                           (fixed_arity ?relation)) 'CxCore)]
        (is (some? h))
        (is (some? rule))
        (is (some (fn [d]
                    (and (= rule (:informant d))
                         (some #(= (list 'arity barRelation 3)
                                   (:sentence (v/sentex kb %)))
                               (:antecedents d))))
                  (v/supporting-justifications kb h)))
        (is (v/isa? kb barRelation 'ternary))
        (is (not (v/isa? kb barRelation 'ternary_predicate)))))))

(tu/deftest-kb arity-conclusions-retract-with-their-declarations
  (testing "asserting the arity keeps the arity, the class and the policy believed"
    (tu/with-terms [binaryRelation]
      (let [h (v/assert kb (list 'arity binaryRelation 2) 'CxCore)]
        (is (seq (v/sentexes-matching kb (list 'arity binaryRelation 2) 'CxCore)))
        (is (seq (v/sentexes-matching kb (list 'fixed_arity binaryRelation) 'CxCore)))
        (is (seq (v/sentexes-matching kb (list 'binary binaryRelation) 'CxCore)))
        (testing "and retracting the sole premise collapses the whole cycle"
          (v/retract! kb h)
          (is (empty? (v/sentexes-matching kb (list 'binary binaryRelation) 'CxCore)))
          (is (empty? (v/sentexes-matching kb (list 'fixed_arity binaryRelation) 'CxCore)))
          (is (empty? (v/sentexes-matching kb (list 'arity binaryRelation 2) 'CxCore)))))))
  (testing "a specialized predicate witness keeps its type, arity, and predicate policy"
    (tu/with-terms [qPred]
      (let [h (v/assert kb (list 'unary_predicate qPred) 'CxCore)]
        (is (v/isa? kb qPred 'unary))
        (is (v/isa? kb qPred 'fixed_arity_predicate))
        (is (= 1 (:arity (v/describe kb qPred 'CxCore))))
        (testing "and retracting it takes the arity with the class"
          (v/retract! kb h)
          (is (nil? (:arity (v/describe kb qPred 'CxCore)))))))))

(tu/deftest-kb algebraic-predicate-types-classify-a-predicate
  ;; (symmetric siblingOf) etc. are the marks the provers read AND, since each mark is a
  ;; type — (genl symmetric binary_predicate) in CxCore — the classification itself: the
  ;; property IS the membership, with no derived (…Predicate) twin between them.
  (testing "the mark is a membership in the property type"
    (is (v/isa? kb 'siblingOf 'symmetric))
    (is (v/isa? kb 'marriedTo 'symmetric))
    (is (v/isa? kb 'ancestorOf 'transitive))
    (is (v/isa? kb 'partOf 'transitive))
    (is (v/isa? kb 'birthYearOf 'functional)))
  (testing "and inherit binary_predicate / predicate through genl"
    (is (v/isa? kb 'siblingOf 'binary_predicate))
    (is (v/isa? kb 'ancestorOf 'predicate)))
  (testing "the mark is decontextualized, so it is visible wherever CxUniverse is — every
            data context — while staying out of CxCore's own sight above the universe"
    (is (seq (v/sentexes-matching kb '(symmetric siblingOf) 'CxLife)))
    (is (v/ask? kb '(symmetric siblingOf) 'CxSocialWorld))
    (is (empty? (v/sentexes-matching kb '(symmetric siblingOf) 'CxCore)))))

(tu/deftest-kb algebraic-predicate-types-answer-and-enumerate
  (testing "membership is answered directly from the stored mark"
    (is (v/ask? kb '(symmetric siblingOf)))
    (is (v/ask? kb '(transitive ancestorOf)))
    (is (v/ask? kb '(functional birthYearOf)))
    (is (not (v/ask? kb '(symmetric parentOf)))))
  (testing "and enumerated"
    ;; the domain relations and orthogonal are the symmetric marks, decontextualized like
    ;; the other algebraic marks, so they answer the enumeration wherever CxUniverse is
    ;; seen.  seeAlso is NOT among them — it is a directional cross-reference, not
    ;; symmetric.
    (is (= '#{siblingOf marriedTo friendOf orthogonal siblingDisjointException}
           (set (map #(get % '?p) (v/ask kb '(symmetric ?p) '?ctx)))))
    ;; `genl` and `genlCx` are in the enumeration because CxCore asserts (transitive genl)
    ;; / (transitive genlCx) outright.  They *are* transitive; answering them from cached
    ;; closures instead of the generic prover is an implementation choice, not a difference
    ;; in what they mean — and saying so is what lets them be named as the relation an
    ;; argument position is preserved along (docs/inherit.md).  The declaration is held out
    ;; of the :transitive prop machinery (the closure-relations skip-set), so it stays a
    ;; queryable classification without routing them to the generic prover — which is why
    ;; they do not appear in `(props kb :transitive)`.
    ;; `heavierThan` / `tallerThan` / `olderThan` are the instance-level strict orders,
    ;; transitive beside `largerThan`'s type-level claim: two stated comparisons compose
    ;; into the third off the closure, which is the route a KB that weighed nothing has.
    ;; `greaterInMagnitudeThan` is the fourth, over quantities rather than objects.
    ;; `instantBefore` / `instantAfter` are the point algebra's strict orders, transitive
    ;; so a forward join reads a narrative's consecutive links as one ordering.
    (is (= '#{ancestorOf partOf locatedIn largerThan instantBefore instantAfter
              causes beforeEvent genl genlCx
              heavierThan tallerThan olderThan greaterInMagnitudeThan}
           (set (map #(get % '?p) (v/ask kb '(transitive ?p) '?ctx)))))
    (is (not (v/has-prop? kb :transitive 'genl)))))

(tu/deftest-kb genlcx-is-a-forced-decontextualized-predicate
  (testing "a genlCx edge is forced to live in CxUniverse, wherever asserted"
    (is (seq   (v/sentexes-matching kb '(genlCx CxUniverse CxOrganism) 'CxUniverse)))
    (is (empty? (v/sentexes-matching kb '(genlCx CxUniverse CxOrganism) 'CxCore))))    ; forced away from CxCore
  (testing "the closure is intact — CxCore vocabulary is still visible from CxUniverse"
    (is (v/ask? kb '(binary_predicate genl) 'CxUniverse))))

;; ---- forced_monotonic_predicate: the roster no default reaches ----------------------

(defn- predicate-spelled
  "A fresh predicate spelled camelCase, which a predicate `genl` needs: `tu/with-terms`
  folds a predicate temp to bare lowercase, the spelling a type shares."
  [base]
  (gensym (str "tmp" base)))

(tu/deftest-kb a-default-write-of-each-roster-group-keeps-its-strength
  (tu/with-terms [CxLow parentOf dog_kind cat_kind bird_kind Ann Bob Cal Dan]
    (let [fatherOf (predicate-spelled "FatherOf")
          kinOf    (predicate-spelled "KinOf")
          fact     (v/assert kb (list parentOf Ann Bob) CxLow)
          rows     [["a relation mark" (list 'functional parentOf)             'CxUniverse]
                    ["predicate genl"  (list 'genl fatherOf kinOf)             'CxUniverse]
                    ["a declaration"   (list 'disjoint dog_kind cat_kind)      'CxUniverse]
                    ["except"          (list 'except (sx/sentex-handle fact))  CxLow]
                    ["equality"        (list 'sameAs Cal Dan)                  CxLow]]]
      (doseq [[group s c] rows]
        (testing group
          (is (= :default (v/defeat-class kb (v/assert kb s c)))))))
    (testing "and so does a genl between types"
      (is (= :default (v/defeat-class kb (v/assert kb (list 'genl bird_kind cat_kind) 'CxUniverse)))))
    (testing "a genlCx edge is read :monotonic, so it caps no firing's class"
      (is (= :monotonic (v/defeat-class kb (v/assert kb (list 'genlCx CxLow 'CxUniverse) 'CxUniverse)))))))

(tu/deftest-kb a-late-declaration-leaves-the-premises-before-it-at-their-strength
  (tu/with-terms [likes Ann Bob Cal]
    (let [hs (mapv #(v/assert kb (list likes Ann %) 'CxUniverse) [Bob Cal Ann])]
      (is (= [:default :default :default] (mapv #(v/defeat-class kb %) hs)))
      (v/assert kb (list 'forced_monotonic_predicate likes) 'CxUniverse)
      (is (= [:default :default :default] (mapv #(v/defeat-class kb %) hs))))))

(defn- inert?
  "Is handle `h` stored, not believed, and reported `:inert` by `why-not`."
  [kb h]
  (and (some? (v/sentex kb h)) (not (v/in? kb h)) (= :inert (:reason (v/why-not kb h)))))

(tu/deftest-kb a-denial-of-a-roster-literal-is-stored-inert-in-either-order
  (tu/with-terms [CxLow parentOf dog_kind cat_kind likes Ann Bob Cal Dan]
    (let [fatherOf (predicate-spelled "FatherOf")
          kinOf    (predicate-spelled "KinOf")]
      (testing "beside the literal it denies, which keeps its belief"
        (doseq [s [(list 'genlCx CxLow 'CxUniverse)
                   (list 'functional parentOf)
                   (list 'genl fatherOf kinOf)
                   (list 'disjoint dog_kind cat_kind)
                   (list 'sameAs Ann Bob)]]
          (let [h (v/assert kb s 'CxUniverse)]
            (is (empty? (v/check kb (list 'not s) 'CxUniverse)) (pr-str s))
            (is (inert? kb (v/assert kb (list 'not s) 'CxUniverse)) (pr-str s))
            (is (v/in? kb h) (pr-str s)))))
      (testing "a genl between types is not on the roster, and its denial is believed"
        (is (v/in? kb (v/assert kb (list 'not (list 'genl dog_kind cat_kind)) 'CxUniverse))))
      (testing "a declaration arriving after a stored denial makes it inert"
        (let [d (v/assert kb (list 'not (list likes Cal Dan)) 'CxUniverse)]
          (is (v/in? kb d))
          (v/assert kb (list 'forced_monotonic_predicate likes) 'CxUniverse)
          (is (inert? kb d))
          (is (= d (v/assert kb (list 'not (list likes Cal Dan)) 'CxUniverse))
              "the declaration arriving first stores the same record"))))))

(defn- concluded?
  "`[believed? reported?]`: is `s` believed at `context` (default `CxUniverse`), and is a
  `:forced-conclusion` violation of `s` filed."
  ([kb s] (concluded? kb s 'CxUniverse))
  ([kb s context]
   [(v/ask? kb s context)
    (boolean (some #(and (= :forced-conclusion (:violation %)) (= s (:sentence %)))
                   (v/violations kb)))]))

(tu/deftest-kb a-rule-concludes-a-roster-literal-only-from-roster-antecedents
  (tu/with-terms [strict_kind flies_kind bird_kind parentOf siblingOf Tweety]
    (testing "a non-roster antecedent: the rule is stored, and its firing is dropped and reported"
      (v/assert kb (list 'set/forwardRule (list 'implies (list strict_kind '?p) (list 'asymmetric '?p)))
                'CxUniverse)
      (v/assert kb (list strict_kind parentOf) 'CxUniverse)
      (is (= [false true] (concluded? kb (list 'asymmetric parentOf)))))
    (testing "roster antecedents: the conclusion is believed at its antecedents' class"
      (let [r (v/assert kb (list 'set/forwardRule
                                 (list 'implies (list 'anti_transitive '?p) (list 'irreflexive '?p)))
                        'CxUniverse)]
        (is (= :default (v/defeat-class kb r)) "the rule keeps the strength it was written at")
        (v/assert kb (list 'anti_transitive siblingOf) 'CxUniverse {:strength :monotonic})
        (is (= [true false] (concluded? kb (list 'irreflexive siblingOf))))
        (is (= :monotonic (v/defeat-class kb (v/handle-of kb (list 'irreflexive siblingOf)
                                                          'CxUniverse))))))
    (testing "a declaration arriving after a firing drops it, as arriving first does"
      (v/assert kb (list 'set/forwardRule (list 'implies (list bird_kind '?x) (list flies_kind '?x)))
                'CxUniverse)
      (v/assert kb (list bird_kind Tweety) 'CxUniverse)
      (is (v/ask? kb (list flies_kind Tweety) 'CxUniverse))
      (v/assert kb (list 'forced_monotonic_predicate flies_kind) 'CxUniverse)
      (is (= [false true] (concluded? kb (list flies_kind Tweety)))))))

(tu/deftest-kb an-exception-on-a-roster-rule-makes-its-firings-inert-in-either-order
  ;; a context per order, so the rule and its exception are two sentexes per order
  (doseq [exception-first? [true false]]
    (tu/with-terms [CxHere odd_kind parentOf]
      (let [rule (list 'set/forwardRule
                       (list 'implies (list 'anti_transitive '?p) (list 'irreflexive '?p)))
            exc  #(v/assert kb (list 'exceptWhen (list odd_kind '?p) rule) CxHere)]
        (v/assert kb (list 'genlCx CxHere 'CxUniverse) 'CxUniverse)
        (when exception-first? (exc))
        (v/assert kb rule CxHere)
        (v/assert kb (list 'anti_transitive parentOf) CxHere)
        (when-not exception-first? (exc))
        (is (= [false true] (concluded? kb (list 'irreflexive parentOf) CxHere))
            (str "exception first: " exception-first?))))))

(tu/deftest-kb a-denied-except-still-hides
  ;; `except` is on the forced-monotonic roster: an except keeps the strength it was
  ;; written at and is never a loser, and a denial of one is held OUT, so it keeps hiding
  ;; (docs/nmtms.md, "The forced-monotonic roster").
  (let [ctx (tu/tmp-ctx "Sub") shiny (tu/tmp-pred) gold (tu/tmp-ind)]
    (v/assert kb (list 'genlCx ctx 'CxWell) 'CxUniverse {:strength :monotonic})
    (let [h (v/assert kb (list shiny gold) ctx {:strength :monotonic})
          x (v/assert kb (list 'except (sx/sentex-handle h)) ctx {:strength :default})]
      (is (= :default (v/defeat-class kb x)))
      (is (not (v/ask? kb (list shiny gold) ctx)) "the except hides it")
      (let [d (v/assert kb (list 'not (list 'except (sx/sentex-handle h))) ctx
                        {:strength :monotonic})]
        (is (not (v/in? kb d)) "the denial is held OUT")
        (is (not (v/ask? kb (list shiny gold) ctx)) "and the target stays hidden")))))

(deftest the-verdict-weighs-only-the-members-off-the-roster
  ;; handles 1 and 2 are roster literals: whatever their class, neither is a loser, and a
  ;; nogood with no defeasible member off the roster is `:hard`
  (let [class-of {1 :default 2 :default 3 :default 4 :default 5 :monotonic}
        roster?  #{1 2}]
    (is (= :hard (decide/verdict class-of roster? #{1 2})))
    (is (= :hard (decide/verdict class-of roster? #{1 5})))
    (is (= {:defeat 3} (decide/verdict class-of roster? #{1 3})))
    (is (= :dilemma (decide/verdict class-of roster? #{1 3 4})))))

(deftest a-roster-member-is-never-a-loser-whatever-its-strength
  ;; decide/verdict weighs only the members off the roster: two arity-class memberships
  ;; separated by a `:default` `disjoint` form a `:hard` clash and both stay believed, in
  ;; either order and at either strength.  A bare KB, so no CxCore declaration of the
  ;; separation is re-asserted at `:monotonic`.
  (doseq [membership-strength [:monotonic :default] disjoint-first? [true false]]
    (tu/with-neutral-kb [kb tu/isolated-fresh]
      (let [p    (predicate-spelled "RosterRel")
            decl #(v/assert kb '(disjoint unary binary) 'CxUniverse {:strength :default})
            why  (str membership-strength " memberships, disjoint first: " disjoint-first?)]
        (v/assert kb '(genl unary_predicate unary) 'CxUniverse {:strength :monotonic})
        (v/assert kb '(genl binary_predicate binary) 'CxUniverse {:strength :monotonic})
        (when disjoint-first? (decl))
        (let [ms (mapv #(v/assert kb (list % p) 'CxUniverse {:strength membership-strength})
                       '[unary_predicate binary_predicate])]
          (when-not disjoint-first? (decl))
          (is (every? #(v/ask? kb (:sentence (v/sentex kb %)) 'CxUniverse) ms) why)
          (is (some #(= (set ms) (set (:nogood %))) (v/conflicts kb)) why))))))

(defn- own-ground?
  "Is `literal` among `grounds`, a family's `:grounds` map, read as `decide/roster-literal?`
  reads the roster."
  [grounds literal]
  (let [[f & args] literal]
    (boolean (or (contains? (:forced-monotonic grounds) f)
                 (and (contains? (:forced-between-predicates grounds) f)
                      (every? #(re-matches #"[a-z][a-zA-Z0-9]*[A-Z][a-zA-Z0-9]*" (name %)) args))))))

(deftest a-family-names-its-own-ground-as-a-member-only-beside-other-grounds
  ;; A family reads its `:grounds` as stored marks.  The one nogood that names them as
  ;; members is the arity family's `:arity-descension`, two bindings and nothing else,
  ;; which `decide/verdict` reads `:hard`.  Every family with grounds places its nogoods,
  ;; so each is read off the placed `contradicts`.
  (tu/with-neutral-kb [kb tu/isolated-fresh]
    (tu/with-terms [Ann Bob Cal dog_kind cat_kind Rex]
      (let [[shortRel longRel irrRel antiRel ageRel beatsRel nextRel]
            (mapv predicate-spelled ["Short" "Long" "Irr" "Anti" "Age" "Beats" "Next"])
            U 'CxUniverse]
        (doseq [s [(list 'binary_predicate shortRel) (list shortRel Ann Bob Cal)
                   (list 'ternary_predicate longRel) (list 'genl longRel shortRel)
                   (list 'irreflexive irrRel) (list irrRel Ann Ann)
                   (list 'anti_symmetric antiRel) (list antiRel Ann Bob) (list antiRel Bob Ann)
                   (list 'functional ageRel) (list ageRel Ann 1) (list ageRel Ann 2)
                   (list 'asymmetric beatsRel) (list beatsRel Ann Bob) (list beatsRel Bob Ann)
                   (list 'anti_transitive nextRel) (list nextRel Ann Bob) (list nextRel Bob Cal)
                   (list nextRel Ann Cal)
                   (list 'disjoint dog_kind cat_kind) (list dog_kind Rex) (list cat_kind Rex)]]
          (v/assert kb s U))
        (let [sen     #(:sentence (v/sentex kb %))
              grounds (keep :grounds decide/registry)
              placed  (for [c (v/sentexes-with-functor kb 'contradicts)
                            :let [ms (into #{} (map v/handle-id) (rest (:sentence c)))]]
                        [ms (decide/placed-kind kb ms)])]
          (is (= #{:arity :arity-descension :irreflexive :anti-symmetric :functional :asymmetric
                   :anti-transitive :disjoint}
                 (into #{} (map second) placed))
              "every family with grounds places a nogood here")
          (is (= #{(list cat_kind Rex) (list dog_kind Rex)}
                 (into #{} (comp (filter #(= :disjoint (second %))) (mapcat first) (map sen)) placed))
              "the placed membership nogood names the memberships and not the declaration")
          (doseq [[ms kind] placed
                  :let [own (filterv (fn [h] (some #(own-ground? % (sen h)) grounds)) ms)]
                  :when (seq own)]
            (is (and (= :arity-descension kind) (= (count own) (count ms)))
                (pr-str kind (mapv sen ms)))))))))

(tu/deftest-kb an-except-hides-the-derivation-it-blocks-in-either-order-beside-its-denial
  ;; A denial held OUT moves no belief, so the except and its denial end in one state
  ;; whichever arrives first: the conclusion resting on the hidden fact is swept.
  (let [ctx (tu/tmp-ctx "Sub") qq (tu/tmp-pred) pp (tu/tmp-pred)]
    (v/assert kb (list 'genlCx ctx 'CxWell) 'CxUniverse {:strength :monotonic})
    (v/assert kb (list 'implies (list qq '?x) (list pp '?x)) ctx {:direction :forward})
    (doseq [denial-first? [false true]]
      (tu/with-terms [Aa]
        (let [h     (v/assert kb (list qq Aa) ctx {:strength :monotonic})
              deny! #(v/assert kb (list 'not (list 'except (sx/sentex-handle h))) ctx
                               {:strength :monotonic})]
          (is (seq (v/sentexes-matching kb (list pp Aa) ctx)) "the rule fired")
          (when denial-first? (deny!))
          (v/assert kb (list 'except (sx/sentex-handle h)) ctx {:strength :default})
          (when-not denial-first? (deny!))
          (is (empty? (v/sentexes-matching kb (list pp Aa) ctx))
              (str "the except sweeps the conclusion, denial first: " denial-first?)))))))

(defn- roster-history
  "Assert into CxUniverse a fact and a denial of `likes`, a non-roster rule concluding
  `likes` from `knows`, and the fact that fires it, with a `(forced_monotonic_predicate
  likes)` declaration arriving before the write at index `at` and retracted after the
  last one (no declaration when `at` is nil).  Answers, for the fact, the denial and the
  rule's conclusion, `[believed? defeat-class justification-count]`."
  [kb likes knows Ann Bob Cal Dan at]
  (let [decl   (list 'forced_monotonic_predicate likes)
        writes [#(v/assert kb (list likes Ann Bob) 'CxUniverse)
                #(v/assert kb (list 'not (list likes Ann Cal)) 'CxUniverse)
                #(v/assert kb (list 'set/forwardRule (list 'implies (list knows '?x '?y)
                                                           (list likes '?x '?y)))
                           'CxUniverse)
                #(v/assert kb (list knows Ann Dan) 'CxUniverse)]]
    (doseq [[i w] (map-indexed vector writes)]
      (when (= i at) (v/assert kb decl 'CxUniverse))
      (w))
    (when at (v/retract! kb (v/handle-of kb decl 'CxUniverse)))
    (mapv (fn [s] (let [h (v/handle-of kb s 'CxUniverse)]
                    [(boolean (and h (v/in? kb h))) (when h (v/defeat-class kb h))
                     (if h (count (v/supporting-justifications kb h)) 0)]))
          [(list likes Ann Bob) (list 'not (list likes Ann Cal)) (list likes Ann Dan)])))

(tu/deftest-kb retracting-a-declaration-leaves-the-belief-a-kb-that-never-had-it-holds
  (let [never (tu/with-terms [likes knows Ann Bob Cal Dan]
                (roster-history kb likes knows Ann Bob Cal Dan nil))]
    (is (= [[true :default 0] [true :default 0] [true :default 1]] never))
    (doseq [at (range 4)]
      (tu/with-terms [likes knows Ann Bob Cal Dan]
        (is (= never (roster-history kb likes knows Ann Bob Cal Dan at))
            (str "declared before write " at))))))

;; ---- the declaration is a switch: an oracle over random histories -------------------

(defn- shuffle-with
  "`xs` in an order drawn from `rng`."
  [^java.util.Random rng xs]
  (let [l (java.util.ArrayList. ^java.util.Collection (vec xs))]
    (java.util.Collections/shuffle l rng)
    (vec l)))

(defn- roster-world
  "A random history over abstract terms, from `seed`: writes of two predicates `:p` and
  `:q` and a non-roster `:k` over four individuals in two contexts, a rule concluding
  `:p` from `:k` and one concluding `:q` from `:p` (a roster rule while both are
  declared), and `forced_monotonic_predicate` declarations of `:p` and `:q` arriving at
  random points, half of them retracted later.  Answers `{:history [[op form ctx
  strength] …] :final [[form ctx strength] …]}`: `:final` is the content the history
  ends with, in an order of its own."
  [seed]
  (let [rng   (java.util.Random. seed)
        pick  #(nth % (.nextInt rng (count %)))
        inds  [:a :b :c :d]
        write (fn []
                (case (.nextInt rng 5)
                  (0 1) [(list (pick [:p :q]) (pick inds) (pick inds)) (pick [:cx-top :cx-low])]
                  2     [(list 'not (list (pick [:p :q]) (pick inds) (pick inds)))
                         (pick [:cx-top :cx-low])]
                  (3 4) [(list :k (pick inds) (pick inds)) (pick [:cx-top :cx-low])]))
        rules [[(list 'set/forwardRule (list 'implies (list :k '?x '?y) (list :p '?x '?y))) :cx-top]
               [(list 'set/forwardRule (list 'implies (list :p '?x '?y) (list :q '?x '?y))) :cx-top]]
        ws    (mapv #(conj % (pick [:default :default :monotonic]))
                    (shuffle-with rng (into rules (distinct) (repeatedly (+ 4 (.nextInt rng 6)) write))))
        decls (for [f [:p :q] :when (pos? (.nextInt rng 3))]
                [(list 'forced_monotonic_predicate f) :cx-top :default])
        hist  (reduce (fn [h d]
                        (let [at (.nextInt rng (inc (count h)))
                              h  (-> (subvec h 0 at) (conj (into [:assert] d)) (into (subvec h at)))]
                          (if (zero? (.nextInt rng 2))
                            (let [later (+ at 1 (.nextInt rng (- (count h) at)))]
                              (-> (subvec h 0 later) (conj (into [:retract] d)) (into (subvec h later))))
                            h)))
                      (mapv #(into [:assert] %) ws)
                      decls)
        gone  (into #{} (comp (filter #(= :retract (first %))) (map #(subvec % 1))) hist)]
    {:history hist
     :final   (shuffle-with rng (into [] (comp (filter #(= :assert (first %))) (map #(subvec % 1))
                                               (remove gone))
                                      hist))}))

(defn- run-world
  "Run `steps` (`[op form ctx strength]`) over fresh temporaries, `:cx-low` below
  `:cx-top`, and answer `[believed? defeat-class]` for every literal of the three
  predicates and its denial at both contexts, keyed by the abstract terms."
  [kb steps]
  (tu/with-terms [likes trusts knows Ann Bob Cal Dan CxTop CxLow]
    (let [terms {:p likes :q trusts :k knows :a Ann :b Bob :c Cal :d Dan
                 :cx-top CxTop :cx-low CxLow}
          inst  #(walk/postwalk (fn [x] (get terms x x)) %)]
      (v/assert kb (list 'genlCx CxLow CxTop) 'CxUniverse)
      (doseq [[op form ctx strength] steps]
        (case op
          :assert  (v/assert kb (inst form) (terms ctx) {:strength strength})
          :retract (v/retract! kb (v/handle-of kb (inst form) (terms ctx)))))
      (into {} (for [f [:p :q :k] a [:a :b :c :d] b [:a :b :c :d] neg? [false true]
                     c [:cx-top :cx-low]
                     :let [s (inst (cond->> (list f a b) neg? (list 'not)))
                           h (v/handle-of kb s (terms c))]]
                 [[f a b neg? c] [(v/ask? kb s (terms c)) (when h (v/defeat-class kb h))]])))))

(defn- declaration-oracle
  "The oracle for the switch over the worlds of `seeds`: declarations arriving and
  leaving at random points give the beliefs and classes the final content gives when
  asserted fresh, in another order (docs/nmtms.md, \"The forced-monotonic roster\")."
  [kb seeds]
  (doseq [seed seeds]
    (let [{:keys [history final]} (roster-world seed)]
      (is (= (run-world kb (mapv #(into [:assert] %) final)) (run-world kb history))
          (str "seed " seed)))))

(tu/deftest-kb a-declaration-history-believes-what-its-final-content-believes
  ;; the sampled twin of the sweep below: seed 1 declares both predicates and retracts one
  (declaration-oracle kb [1]))

(tu/deftest-kb ^:slow every-declaration-history-believes-what-its-final-content-believes
  (declaration-oracle kb (range 60)))

(defn- switches
  "How many times `checks/force-reach!` runs while `f` runs."
  [f]
  (let [n (atom 0) orig checks/force-reach!]
    (with-redefs [checks/force-reach! (fn [& args] (swap! n inc) (apply orig args))]
      (f))
    @n))

(deftest a-declaration-of-a-baseline-member-runs-no-switch
  ;; a bare KB: the engine's baseline already holds `sameAs`, so its declaration moves no
  ;; membership, while `likes` joins and leaves the roster by its declaration, which a
  ;; stored denial of each reads: held OUT while its literal is on the roster
  (tu/with-neutral-kb [kb tu/isolated-fresh]
    (tu/with-terms [likes Ann Bob Cal Dan]
      (let [same  (v/assert kb (list 'not (list 'sameAs Ann Bob)) 'CxUniverse)
            fact  (v/assert kb (list 'not (list likes Cal Dan)) 'CxUniverse)
            decl  (list 'forced_monotonic_predicate likes)
            in    #(mapv (partial v/in? kb) [same fact])]
        (is (= [false true] (in)))
        (is (= 0 (switches #(v/assert kb '(forced_monotonic_predicate sameAs) 'CxUniverse))))
        (is (= 1 (switches #(v/assert kb decl 'CxUniverse))))
        (is (= [false false] (in)))
        (is (= 1 (switches #(v/retract! kb (v/handle-of kb decl 'CxUniverse)))))
        (is (= [false true] (in)))))))

(tu/deftest-kb an-uncleared-declaration-is-not-retracted
  (testing "genlCx is always forced: its declaration stays"
    (let [h (v/handle-of kb '(forced_monotonic_predicate genlCx) 'CxCore)
          e (try (v/retract! kb h) nil (catch clojure.lang.ExceptionInfo e (ex-data e)))]
      (is (= [:uncleared-forcing 'genlCx :unforced-context-edge]
             ((juxt :type :predicate :missing) e)))
      (is (v/in? kb h))))
  (testing "a declaration whose predicate has no unforced semantics stays"
    (doseq [[s missing] [['(forced_monotonic_predicate disjoint) :unforced-definitional-declaration]
                         ['(forced_monotonic_predicate sameAs) :unforced-equality]
                         ['(forced_monotonic_predicate except) :unforced-except]
                         ['(forced_monotonic_between_predicates genl) :unforced-predicate-genl]]]
      (let [h (v/handle-of kb s 'CxCore)]
        (is (= [:uncleared-forcing missing]
               ((juxt :type :missing)
                (try (v/retract! kb h) nil (catch clojure.lang.ExceptionInfo e (ex-data e)))))
            (pr-str s))
        (is (v/in? kb h) (pr-str s)))))
  (testing "injection, surjection, bijection and an author's own predicate are cleared"
    (tu/with-terms [likes]
      (doseq [p ['injection 'surjection 'bijection likes]]
        (is (not (contains? checks/uncleared-forcing ['forced_monotonic_predicate p]))
            (str p))))))

;; ---- decontextualized_predicate: a fact that belongs to the KB, not to one theory --
;;
;; `(decontextualized_predicate P)` deduces every `(P ...)` into CxUniverse, which
;; every context sees.  CxUniverse and not a named target: the definitional
;; checks are context-scoped and run where the fact is stated, so a target the stating
;; context cannot see is a place those checks never look — two facts, each admissible
;; where it was stated, could meet there as a disjointness violation nothing reports.

(tu/deftest-kb a-decontextualized-fact-reaches-the-universe
  ;; Declaration first, then the fact — the forward path, where the lift runs as the
  ;; fact is stored (the retroactive half is the next test).  The demonstration builds
  ;; its own predicate: the shipped ontology declares the *metadata* marks and
  ;; genlCx and nothing else, every one of them a claim about a predicate rather
  ;; than about a world, so there is no shipped fact to lift.
  (tu/with-terms [rulesOver ridesWith Ann Bob CxAlpha CxBeta]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'genlCx CxBeta 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'decontextualized_predicate rulesOver) 'CxUniverse)
    (v/assert kb (list rulesOver Ann Bob) CxAlpha)
    (v/assert kb (list ridesWith Ann Bob) CxAlpha)

    (testing "the fact is copied into CxUniverse"
      (is (seq (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse))))
    (testing "visible from a sibling context (via CxUniverse), unlike an undeclared one"
      (is (v/ask? kb (list rulesOver Ann Bob) CxBeta))
      (is (not (v/ask? kb (list ridesWith Ann Bob) CxBeta))))
    (testing "the copy is justified by the placement sentex and the declaration"
      (let [u (v/handle-of kb (list rulesOver Ann Bob) 'CxUniverse)
            d (first (v/supporting-justifications kb u))]
        (is (= 'decontextualized_predicate (:informant d)))
        (is (= 2 (count (:antecedents d))))
        (is (some #(= (list 'decontextualized_predicate rulesOver) (:sentence (v/sentex kb %)))
                  (:antecedents d)))))
    (testing "and the declaration reads back as predicate metadata"
      (is (v/has-prop? kb :decontextualized rulesOver))
      (is (not (v/has-prop? kb :decontextualized ridesWith)))
      (is (contains? (v/props kb :decontextualized) rulesOver)))))

(tu/deftest-kb the-shipped-ontology-decontextualizes-predicate-metadata-and-nothing-else
  ;; What carries the mark is a claim about a *predicate* — its algebra — plus
  ;; genlCx by force.  A domain relation carrying it would make one theory's fact
  ;; a claim of the whole KB, and would take a rule's conclusions out with it: a
  ;; decontextualized marriage lifts (knows ?x ?y) through CxSocial's rule into a
  ;; context every data context sees, decontextualizing a predicate nothing declared.
  (testing "the roster is the algebraic marks, the inverse declaration, and genlCx"
    ;; The three commutativity marks are here for `symmetric`'s reason and one of its
    ;; own: they are predicate algebra, and `res/kb-sentex` reads them **globally** —
    ;; a sentex has one key, so whether a predicate sorts its arguments cannot vary by
    ;; reader.  A context-scoped declaration behind a global read would be a mark
    ;; visible from one context and acted on from every one.
    ;; `functionalInArg` is `functional` read at another position, and lifted with it.
    (is (= '#{functional functionalInArg inverse reflexive symmetric asymmetric transitive
              irreflexive anti_symmetric anti_transitive equivalence_relation
              injection surjection bijection
              commutative commutativeInArgs commutativeInArgAndRest}
           (v/props kb :decontextualized)))
    (is (= '#{genlCx} (v/props kb :forced-decontextualized))))
  (testing "so a social fact stays in the theory that states it"
    (is (not (v/has-prop? kb :decontextualized 'marriedTo)))
    (is (empty? (v/sentexes-matching kb '(marriedTo Bob Nancy) 'CxUniverse)))
    (is (not (v/ask? kb '(marriedTo Bob Nancy) 'CxNaturalWorld)))
    (is (not (v/ask? kb '(owns Tom Car1) 'CxNaturalWorld))))
  (testing "and the knows-fact its rule concludes stays there with it"
    (is (v/ask? kb '(knows Bob Nancy) 'CxSocialWorld))
    (is (not (v/ask? kb '(knows Bob Nancy) 'CxNaturalWorld)))))

(tu/deftest-kb declaring-it-lifts-facts-already-asserted
  ;; The retroactive sweep, the half a declaration-then-facts test never exercises: a
  ;; broken one looks like it works for everything asserted afterwards.
  (tu/with-terms [rulesOver Ann Bob Cid Dee CxAlpha]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list rulesOver Ann Bob) CxAlpha)
    (testing "before the declaration the fact is confined to its own context"
      (is (empty? (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse))))

    (v/assert kb (list 'decontextualized_predicate rulesOver) 'CxUniverse)
    (testing "declaring it lifts the fact that was already there"
      (is (seq (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse))))
    (testing "and facts asserted afterwards are lifted too"
      (v/assert kb (list rulesOver Cid Dee) CxAlpha)
      (is (seq (v/sentexes-matching kb (list rulesOver Cid Dee) 'CxUniverse))))))

(tu/deftest-kb retracting-the-declaration-withdraws-the-lifted-copies
  (tu/with-terms [rulesOver Ann Bob CxAlpha]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (let [dh (v/assert kb (list 'decontextualized_predicate rulesOver) 'CxUniverse)]
      (v/assert kb (list rulesOver Ann Bob) CxAlpha)
      (is (seq (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse)))

      (v/retract! kb dh)
      (testing "the copy goes with the declaration that licensed it"
        (is (empty? (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse))))
      (testing "and the metadata follows"
        (is (not (v/has-prop? kb :decontextualized rulesOver))))
      (testing "the fact itself is untouched — only the copy rested on the declaration"
        (is (seq (v/sentexes-matching kb (list rulesOver Ann Bob) CxAlpha)))))))

;; ---- the lift reaches derived content, in any order ---------------------
;;
;; A decontextualized predicate is a claim about the *predicate*, so what a rule
;; concludes is lifted exactly as what a caller asserts.  Lifting only asserted content
;; made belief depend on arrival order — declare-then-derive left the conclusion where
;; it was concluded, while derive-then-declare lifted it through the retroactive sweep —
;; and the two are the same knowledge.

(tu/deftest-kb a-derived-conclusion-is-lifted-like-an-asserted-fact
  (tu/with-terms [bornInFrance speaksFrench Ann CxAlpha]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'decontextualized_predicate speaksFrench) 'CxUniverse)
    (v/assert-rule kb [(list bornInFrance '?x)] (list speaksFrench '?x) CxAlpha {:direction :forward})
    (v/assert kb (list bornInFrance Ann) CxAlpha)

    (testing "the rule concludes in its own context"
      (is (seq (v/sentexes-matching kb (list speaksFrench Ann) CxAlpha))))
    (testing "and the conclusion is lifted, exactly as an asserted fact would be"
      (is (seq (v/sentexes-matching kb (list speaksFrench Ann) 'CxUniverse))))
    (testing "the copy names the declaration that licensed it, so `why` points at what to retract"
      (let [u (v/handle-of kb (list speaksFrench Ann) 'CxUniverse)
            d (first (v/supporting-justifications kb u))]
        (is (= 'decontextualized_predicate (:informant d)))
        (is (some #(= (list 'decontextualized_predicate speaksFrench)
                      (:sentence (v/sentex kb %)))
                  (:antecedents d)))))
    (testing "retracting the fact takes the conclusion and its copy with it"
      (v/retract! kb (v/handle-of kb (list bornInFrance Ann) CxAlpha))
      (is (empty? (v/sentexes-matching kb (list speaksFrench Ann) CxAlpha)))
      (is (empty? (v/sentexes-matching kb (list speaksFrench Ann) 'CxUniverse))))))

(tu/deftest-kb the-lift-is-independent-of-the-order-its-parts-arrive
  ;; Declaration, rule and fact in all six orders: same beliefs, or the engine's
  ;; order-independence invariant is broken by the lift.
  (let [outcomes (mapv (fn [order]
                         (tu/with-terms [bornInFrance speaksFrench Ann CxAlpha]
                           (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
                           (doseq [step order]
                             (case step
                               :decl (v/assert kb (list 'decontextualized_predicate speaksFrench)
                                               'CxUniverse)
                               :rule (v/assert-rule kb [(list bornInFrance '?x)] (list speaksFrench '?x) CxAlpha {:direction :forward})
                               :fact (v/assert kb (list bornInFrance Ann) CxAlpha)))
                           [(boolean (seq (v/sentexes-matching kb (list speaksFrench Ann) CxAlpha)))
                            (boolean (seq (v/sentexes-matching kb (list speaksFrench Ann) 'CxUniverse)))]))
                       [[:decl :rule :fact] [:decl :fact :rule]
                        [:rule :decl :fact] [:rule :fact :decl]
                        [:fact :decl :rule] [:fact :rule :decl]])]
    (is (= [[true true]] (distinct outcomes))
        "every order concludes in the rule's context and lifts into CxUniverse")))

(tu/deftest-kb a-rule-over-a-lifted-predicate-reaches-a-fixpoint
  ;; The copy is a new datum in a context the rule can see, so it goes back on the
  ;; agenda and the rule fires on it.  Justification dedup is what stops that being a
  ;; loop: re-deriving a sentence already stored adds no handle, so the agenda drains.
  (tu/with-terms [connects A B C D CxAlpha]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'decontextualized_predicate connects) 'CxUniverse)
    (v/assert-rule kb [(list connects '?x '?y) (list connects '?y '?z)]
                   (list connects '?x '?z) CxAlpha {:direction :forward})
    (v/assert kb (list connects A B) CxAlpha)
    (v/assert kb (list connects B C) CxAlpha)
    (v/assert kb (list connects C D) CxAlpha)
    (testing "the transitive closure of a 3-edge path is 6 edges, derived once"
      (is (= 6 (count (v/sentexes-matching kb (list connects '?x '?y) CxAlpha))))
      (is (= 6 (count (v/sentexes-matching kb (list connects '?x '?y) 'CxUniverse)))))
    (testing "and the run completed rather than hitting the depth guard"
      (is (not (:truncated? (:last (v/chain-stats kb))))))))

(tu/deftest-kb an-exception-in-the-universe-sees-the-lifted-copy
  ;; The copy goes through the derivation-path choke point, so its arrival is a
  ;; re-check trigger like any other fact's.  Without that the exception would never be
  ;; re-evaluated and the conclusion it should block would stand.
  (tu/with-terms [bird flies penguin Opus CxAlpha]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'decontextualized_predicate penguin) 'CxUniverse)
    (v/assert kb (list 'exceptWhen (list penguin '?x)
                       (list 'set/defaultRule
                             (list 'set/forwardRule (list 'implies (list 'and (list bird '?x)) (list flies '?x)))))
              'CxUniverse)
    (v/assert kb (list bird Opus) 'CxUniverse)
    (is (v/ask? kb (list flies Opus) 'CxUniverse) "nothing excepts it yet")

    ;; the penguin fact is stated in a context the rule cannot see; only the lift
    ;; brings it into range
    (v/assert kb (list penguin Opus) CxAlpha)
    (is (seq (v/sentexes-matching kb (list penguin Opus) 'CxUniverse)) "lifted into the rule's context")
    (is (not (v/ask? kb (list flies Opus) 'CxUniverse))
        "the arriving copy re-triggered the exception, which now blocks")))

(tu/deftest-kb two-declarations-are-two-witnesses-for-one-copy
  ;; The declaration is not forced-decontextualized, so the same claim stated in two
  ;; contexts is two sentexes.  One copy, justified once per declaration — as a migrated
  ;; twin is justified once per equality — so dropping one leaves the copy standing.
  (tu/with-terms [rulesOver Ann Bob CxAlpha CxBeta]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'genlCx CxBeta 'CxUniverse) 'CxUniverse)
    (let [d1 (v/assert kb (list 'decontextualized_predicate rulesOver) CxAlpha)
          d2 (v/assert kb (list 'decontextualized_predicate rulesOver) CxBeta)]
      (is (not= d1 d2) "two contexts, two sentexes")
      (v/assert kb (list rulesOver Ann Bob) CxAlpha)
      (let [u (v/handle-of kb (list rulesOver Ann Bob) 'CxUniverse)]
        (is (= 2 (count (v/supporting-justifications kb u))) "one witness per declaration")
        (v/retract! kb d1)
        (is (seq (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse))
            "the copy stands on the surviving declaration")
        (v/retract! kb d2)
        (is (empty? (v/sentexes-matching kb (list rulesOver Ann Bob) 'CxUniverse))
            "and goes when the last one does")))))

(tu/deftest-kb a-consequence-of-a-lifted-fact-is-lifted-in-turn
  ;; The copy is a chaining seed, and what that provides is **placement**: forward chaining
  ;; already matches antecedents across contexts, so firing on the copy does not find
  ;; anything new — it places the conclusion in CxUniverse rather than only in the
  ;; context the fact came from.  A consequence of a fact true everywhere is true
  ;; everywhere too.
  (tu/with-terms [edgeTo reachesFrom A B CxAlpha CxSibling]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'genlCx CxSibling 'CxUniverse) 'CxUniverse)
    (v/assert-rule kb [(list edgeTo '?x '?y)] (list reachesFrom '?y '?x) 'CxUniverse {:direction :forward})
    (v/assert kb (list 'decontextualized_predicate edgeTo) 'CxUniverse)
    (v/assert kb (list edgeTo A B) CxAlpha)
    (testing "the conclusion is placed both where the fact was stated and in the universe"
      (is (= #{CxAlpha 'CxUniverse}
             (set (map :context (v/sentexes-matching kb (list reachesFrom B A) '?ctx))))))
    (testing "so a sibling context sees it, which it would not without the lift"
      (is (v/ask? kb (list reachesFrom B A) CxSibling)))))

(tu/deftest-kb a-negative-fact-is-not-lifted
  ;; `(not (P a))` has functor `not`, so a declaration about `P` does not reach it: the
  ;; positive extent becomes universal and the negative one stays in its context.
  ;; Deliberate, and pinned here so changing it has to be a decision.
  (tu/with-terms [flies Tweety Opus CxAlpha]
    (v/assert kb (list 'genlCx CxAlpha 'CxUniverse) 'CxUniverse)
    (v/assert kb (list 'decontextualized_predicate flies) 'CxUniverse)
    (v/assert kb (list flies Tweety) CxAlpha)
    (v/assert kb (list 'not (list flies Opus)) CxAlpha)
    (is (seq (v/sentexes-matching kb (list flies Tweety) 'CxUniverse)) "the positive literal lifts")
    (is (empty? (v/sentexes-matching kb (list 'not (list flies Opus)) 'CxUniverse))
        "the negative literal does not")))

(tu/deftest-kb a-lift-out-of-sight-of-the-universe-stores-a-clashing-copy-and-reports-it
  ;; The definitional checks run where a fact is stated, against what is visible from
  ;; there, so they cover the CxUniverse copy whenever the stating context sees
  ;; CxUniverse.  Two contexts wired outside the spindle do not: neither sees the other's
  ;; fact, so each assert passes and the copies meet in CxUniverse.  The copy is admitted
  ;; as a firing's conclusion is: the clash is stored and decided there, in either order.
  (tu/with-terms [dog cat Rex CxOffA CxOffB]
    (v/assert kb (list 'genlCx CxOffA 'CxCore) 'CxUniverse)
    (v/assert kb (list 'genlCx CxOffB 'CxCore) 'CxUniverse)
    (is (not (v/sees? kb CxOffA 'CxUniverse)) "wired outside the spindle")
    (v/assert kb (list 'genl dog 'thing) 'CxCore)
    (v/assert kb (list 'genl cat 'thing) 'CxCore)
    (v/assert kb (list 'disjoint dog cat) 'CxCore)
    (v/assert kb (list 'decontextualized_predicate dog) 'CxUniverse)
    (v/assert kb (list 'decontextualized_predicate cat) 'CxUniverse)
    (v/clear-violations! kb)

    (v/assert kb (list dog Rex) CxOffA)
    (testing "each fact is admissible where it is stated — neither context sees the other"
      (is (v/assert kb (list cat Rex) CxOffB)))
    (testing "both copies are stored in CxUniverse"
      (is (seq (v/sentexes-matching kb (list dog Rex) 'CxUniverse)))
      (is (seq (v/sentexes-matching kb (list cat Rex) 'CxUniverse))))
    (testing "and the clash is a reported dilemma, with nothing filed in violations"
      (is (some #(and (= :disjoint (:kind %))
                      (= #{(list dog Rex) (list cat Rex)}
                         (into #{} (map :sentence) (:sides %)))
                      (every? #{'CxUniverse} (map :context (:sides %))))
                (v/contradictions kb)))
      (is (not-any? #(= :disjoint (:violation %)) (v/violations kb))))))

(tu/deftest-kb the-declaration-marks-a-predicate-and-takes-one-argument
  ;; It routes through `prop-problems` like the other unary metadata marks: a second
  ;; argument is not a target to lift into, it is a mistake.
  (tu/with-terms [rulesOver Somewhere]
    (testing "an individual is not a predicate"
      (is (= :not-well-formed
             (:type (try (v/assert kb (list 'decontextualized_predicate Somewhere) 'CxUniverse)
                         (catch clojure.lang.ExceptionInfo e (ex-data e)))))))
    (testing "and it takes exactly one argument"
      ;; :naming, not :not-well-formed — the property is snake_case now, so a second
      ;; argument is refused by the naming check before `wff` counts them
      (is (= :naming
             (:type (try (v/assert kb (list 'decontextualized_predicate rulesOver 'CxUniverse)
                                   'CxUniverse)
                         (catch clojure.lang.ExceptionInfo e (ex-data e)))))))))
